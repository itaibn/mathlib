/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl

Theory of topological rings.
-/
import topology.algebra.group
import ring_theory.ideal.basic
import ring_theory.subring
import algebra.ring.prod

/-!

# Topological (semi)rings

A topological (semi)ring is a (semi)ring equipped with a topology such that all operations are
continuous. Besides this definition, this file proves that the topological closure of a subring
(resp. an ideal) is a subring (resp. an ideal) and defines products and quotients
of topological (semi)rings.

## Main Results

- `subring.topological_closure`/`subsemiring.topological_closure`: the topological closure of a
  `subring`/`subsemiring` is itself a `sub(semi)ring`.
- `prod_ring`/`prod_semiring`: The product of two topological (semi)rings.
- `ideal.closure`: The closure of an ideal is an ideal.
- `topological_ring_quotient`: The quotient of a topological ring by an ideal is a topological ring.

-/

open classical set filter topological_space function
open_locale classical topological_space filter

section topological_ring
variables (Œ± : Type*)

/-- A topological semiring is a semiring where addition and multiplication are continuous. -/
class topological_semiring [topological_space Œ±] [semiring Œ±]
  extends has_continuous_add Œ±, has_continuous_mul Œ± : Prop

section
variables {Œ±} [topological_space Œ±] [semiring Œ±] [topological_semiring Œ±]

/-- The (topological-space) closure of a subsemiring of a topological semiring is
itself a subsemiring. -/
def subsemiring.topological_closure (s : subsemiring Œ±) : subsemiring Œ± :=
{ carrier := closure (s : set Œ±),
  ..(s.to_submonoid.topological_closure),
  ..(s.to_add_submonoid.topological_closure ) }

@[simp] lemma subsemiring.topological_closure_coe (s : subsemiring Œ±) :
  (s.topological_closure : set Œ±) = closure (s : set Œ±) :=
rfl

instance subsemiring.topological_closure_topological_semiring (s : subsemiring Œ±) :
  topological_semiring (s.topological_closure) :=
{ ..s.to_add_submonoid.topological_closure_has_continuous_add,
  ..s.to_submonoid.topological_closure_has_continuous_mul }

lemma subsemiring.subring_topological_closure (s : subsemiring Œ±) :
  s ‚â§ s.topological_closure :=
subset_closure

lemma subsemiring.is_closed_topological_closure (s : subsemiring Œ±) :
  is_closed (s.topological_closure : set Œ±) :=
by convert is_closed_closure

lemma subsemiring.topological_closure_minimal
  (s : subsemiring Œ±) {t : subsemiring Œ±} (h : s ‚â§ t) (ht : is_closed (t : set Œ±)) :
  s.topological_closure ‚â§ t :=
closure_minimal h ht

/-- The product topology on the cartesian product of two topological semirings
  makes the product into a topological semiring. -/
instance prod_semiring {Œ≤ : Type*}
  [semiring Œ≤] [topological_space Œ≤] [topological_semiring Œ≤] : topological_semiring (Œ± √ó Œ≤) :=
{}

end

/-- A topological ring is a ring where the ring operations are continuous. -/
class topological_ring [topological_space Œ±] [ring Œ±]
  extends has_continuous_add Œ±, has_continuous_mul Œ± : Prop :=
(continuous_neg : continuous (Œªa:Œ±, -a))

variables {Œ±} [ring Œ±] [topological_space Œ±]

lemma topological_ring.of_add_group_of_nhds_zero [topological_add_group Œ±]
  (hmul : tendsto (uncurry ((*) : Œ± ‚Üí Œ± ‚Üí Œ±)) ((ùìù 0) √ó·∂† (ùìù 0)) $ ùìù 0)
  (hmul_left : ‚àÄ (x‚ÇÄ : Œ±), tendsto (Œª x : Œ±, x‚ÇÄ * x) (ùìù 0) $ ùìù 0)
  (hmul_right : ‚àÄ (x‚ÇÄ : Œ±), tendsto (Œª x : Œ±, x * x‚ÇÄ) (ùìù 0) $ ùìù 0) : topological_ring Œ± :=
begin
  refine {..‚Äπtopological_add_group Œ±‚Ä∫, ..},
  have hleft : ‚àÄ x‚ÇÄ : Œ±, ùìù x‚ÇÄ = map (Œª x, x‚ÇÄ + x) (ùìù 0), by simp,
  have hadd : tendsto (uncurry ((+) : Œ± ‚Üí Œ± ‚Üí Œ±)) ((ùìù 0) √ó·∂† (ùìù 0)) (ùìù 0),
  { rw ‚Üê nhds_prod_eq,
    convert continuous_add.tendsto ((0 : Œ±), (0 : Œ±)),
    rw zero_add },
  rw continuous_iff_continuous_at,
  rintro ‚ü®x‚ÇÄ, y‚ÇÄ‚ü©,
  rw [continuous_at, nhds_prod_eq, hleft x‚ÇÄ, hleft y‚ÇÄ, hleft (x‚ÇÄ*y‚ÇÄ), filter.prod_map_map_eq,
      tendsto_map'_iff],
  suffices :
    tendsto ((Œª (x : Œ±), x + x‚ÇÄ * y‚ÇÄ) ‚àò (Œª (p : Œ± √ó Œ±), p.1 + p.2) ‚àò
              (Œª (p : Œ± √ó Œ±), (p.1*y‚ÇÄ + x‚ÇÄ*p.2, p.1*p.2)))
            ((ùìù 0) √ó·∂† (ùìù 0)) (map (Œª (x : Œ±), x + x‚ÇÄ * y‚ÇÄ) $ ùìù 0),
  { convert this using 1,
    { ext, simp only [comp_app, mul_add, add_mul], abel },
    { simp only [add_comm] } },
  refine tendsto_map.comp (hadd.comp (tendsto.prod_mk _ hmul)),
  exact hadd.comp (((hmul_right y‚ÇÄ).comp tendsto_fst).prod_mk ((hmul_left  x‚ÇÄ).comp tendsto_snd))
end

lemma topological_ring.of_nhds_zero
  (hadd : tendsto (uncurry ((+) : Œ± ‚Üí Œ± ‚Üí Œ±)) ((ùìù 0) √ó·∂† (ùìù 0)) $ ùìù 0)
  (hneg : tendsto (Œª x, -x : Œ± ‚Üí Œ±) (ùìù 0) (ùìù 0))
  (hmul : tendsto (uncurry ((*) : Œ± ‚Üí Œ± ‚Üí Œ±)) ((ùìù 0) √ó·∂† (ùìù 0)) $ ùìù 0)
  (hmul_left : ‚àÄ (x‚ÇÄ : Œ±), tendsto (Œª x : Œ±, x‚ÇÄ * x) (ùìù 0) $ ùìù 0)
  (hmul_right : ‚àÄ (x‚ÇÄ : Œ±), tendsto (Œª x : Œ±, x * x‚ÇÄ) (ùìù 0) $ ùìù 0)
  (hleft : ‚àÄ x‚ÇÄ : Œ±, ùìù x‚ÇÄ = map (Œª x, x‚ÇÄ + x) (ùìù 0)) : topological_ring Œ± :=
begin
  haveI := topological_add_group.of_comm_of_nhds_zero hadd hneg hleft,
  exact topological_ring.of_add_group_of_nhds_zero hmul hmul_left hmul_right
end

section
variables [t : topological_ring Œ±]
@[priority 100] -- see Note [lower instance priority]
instance topological_ring.to_topological_semiring : topological_semiring Œ± := {..t}

@[priority 100] -- see Note [lower instance priority]
instance topological_ring.to_topological_add_group : topological_add_group Œ± := {..t}
end

variables [topological_ring Œ±]

/-- The product topology on the cartesian product of two topological rings
  makes the product into a topological ring. -/
instance prod_ring {Œ≤ : Type*}
  [ring Œ≤] [topological_space Œ≤] [topological_ring Œ≤] : topological_ring (Œ± √ó Œ≤) :=
{ continuous_neg := continuous_neg }

/-- In a topological ring, the left-multiplication `add_monoid_hom` is continuous. -/
lemma mul_left_continuous (x : Œ±) : continuous (add_monoid_hom.mul_left x) :=
continuous_const.mul continuous_id

/-- In a topological ring, the right-multiplication `add_monoid_hom` is continuous. -/
lemma mul_right_continuous (x : Œ±) : continuous (add_monoid_hom.mul_right x) :=
continuous_id.mul continuous_const

/-- The (topological-space) closure of a subring of a topological semiring is
itself a subring. -/
def subring.topological_closure (S : subring Œ±) : subring Œ± :=
{ carrier := closure (S : set Œ±),
  ..(S.to_submonoid.topological_closure),
  ..(S.to_add_subgroup.topological_closure) }

instance subring.topological_closure_topological_ring (s : subring Œ±) :
  topological_ring (s.topological_closure) :=
{ ..s.to_add_subgroup.topological_closure_topological_add_group,
  ..s.to_submonoid.topological_closure_has_continuous_mul }

lemma subring.subring_topological_closure (s : subring Œ±) :
  s ‚â§ s.topological_closure := subset_closure

lemma subring.is_closed_topological_closure (s : subring Œ±) :
  is_closed (s.topological_closure : set Œ±) := by convert is_closed_closure

lemma subring.topological_closure_minimal
  (s : subring Œ±) {t : subring Œ±} (h : s ‚â§ t) (ht : is_closed (t : set Œ±)) :
  s.topological_closure ‚â§ t := closure_minimal h ht

end topological_ring

section topological_comm_ring
variables {Œ± : Type*} [topological_space Œ±] [comm_ring Œ±] [topological_ring Œ±]

/-- The closure of an ideal in a topological ring as an ideal. -/
def ideal.closure (S : ideal Œ±) : ideal Œ± :=
{ carrier   := closure S,
  smul_mem' := Œª c x hx, map_mem_closure (mul_left_continuous _) hx $ Œª a, S.mul_mem_left c,
  ..(add_submonoid.topological_closure S.to_add_submonoid) }

@[simp] lemma ideal.coe_closure (S : ideal Œ±) : (S.closure : set Œ±) = closure S := rfl

end topological_comm_ring

section topological_ring
variables {Œ± : Type*} [topological_space Œ±] [comm_ring Œ±] (N : ideal Œ±)
open ideal.quotient

instance topological_ring_quotient_topology : topological_space N.quotient :=
by dunfold ideal.quotient submodule.quotient; apply_instance

-- note for the reader: in the following, `mk` is `ideal.quotient.mk`, the canonical map `R ‚Üí R/I`.

variable [topological_ring Œ±]

lemma quotient_ring.is_open_map_coe : is_open_map (mk N) :=
begin
  intros s s_op,
  change is_open (mk N ‚Åª¬π' (mk N '' s)),
  rw quotient_ring_saturate,
  exact is_open_Union (Œª ‚ü®n, _‚ü©, is_open_map_add_left n s s_op)
end

lemma quotient_ring.quotient_map_coe_coe : quotient_map (Œª p : Œ± √ó Œ±, (mk N p.1, mk N p.2)) :=
is_open_map.to_quotient_map
((quotient_ring.is_open_map_coe N).prod (quotient_ring.is_open_map_coe N))
((continuous_quot_mk.comp continuous_fst).prod_mk (continuous_quot_mk.comp continuous_snd))
(by rintro ‚ü®‚ü®x‚ü©, ‚ü®y‚ü©‚ü©; exact ‚ü®(x, y), rfl‚ü©)

instance topological_ring_quotient : topological_ring N.quotient :=
{ continuous_add :=
    have cont : continuous (mk N ‚àò (Œª (p : Œ± √ó Œ±), p.fst + p.snd)) :=
      continuous_quot_mk.comp continuous_add,
    (quotient_map.continuous_iff (quotient_ring.quotient_map_coe_coe N)).mpr cont,
  continuous_neg :=
    by convert continuous_quotient_lift _ (continuous_quot_mk.comp continuous_neg); apply_instance,
  continuous_mul :=
    have cont : continuous (mk N ‚àò (Œª (p : Œ± √ó Œ±), p.fst * p.snd)) :=
      continuous_quot_mk.comp continuous_mul,
    (quotient_map.continuous_iff (quotient_ring.quotient_map_coe_coe N)).mpr cont }

end topological_ring

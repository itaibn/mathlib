import analysis.box_integral.box.subbox_induction
import analysis.box_integral.partition.tagged

namespace box_integral

open set metric
open_locale classical topological_space
noncomputable theory

variables {Î¹ : Type*} [fintype Î¹] {I J : box Î¹}

namespace prepartition

/-- Split a box in `â„â¿` into `2 ^ n` boxes by hyperplanes passing through its center. -/
def split_center (I : box Î¹) : prepartition I :=
{ boxes := finset.univ.map (box.split_center_box_emb I),
  le_of_mem' := by simp [I.split_center_box_le],
  pairwise_disjoint :=
    begin
      rw [finset.coe_map, finset.coe_univ, image_univ],
      rintro _ âŸ¨s, rflâŸ© _ âŸ¨t, rflâŸ© Hne,
      exact I.disjoint_split_center_box (mt (congr_arg _) Hne)
    end }

@[simp] lemma mem_split_center : J âˆˆ split_center I â†” âˆƒ s, I.split_center_box s = J :=
by simp [split_center]

lemma is_partition_split_center (I : box Î¹) : is_partition (split_center I) :=
Î» x hx, by simp [hx]

lemma upper_sub_lower_of_mem_split_center (h : J âˆˆ split_center I) (i : Î¹) :
  J.upper i - J.lower i = (I.upper i - I.lower i) / 2 :=
let âŸ¨s, hsâŸ© := mem_split_center.1 h in hs â–¸ I.upper_sub_lower_split_center_box s i

end prepartition

namespace box

open prepartition tagged_prepartition

@[elab_as_eliminator]
lemma subbox_induction_on {p : box Î¹ â†’ Prop} (I : box Î¹)
  (H_ind : âˆ€ J â‰¤ I, (âˆ€ J' âˆˆ split_center J, p J') â†’ p J)
  (H_nhds : âˆ€ z âˆˆ I.Icc, âˆƒ (U âˆˆ ğ“[I.Icc] z), âˆ€ (J â‰¤ I) (n : â„•), z âˆˆ J.Icc â†’ J.Icc âŠ† U â†’
    (âˆ€ i, J.upper i - J.lower i = (I.upper i - I.lower i) / 2 ^ n) â†’ p J) :
  p I :=
begin
  refine subbox_induction_on' I (Î» J hle hs, H_ind J hle $ Î» J' h', _) H_nhds,
  rcases mem_split_center.1 h' with âŸ¨s, rflâŸ©,
  exact hs s
end

/-- Let `I` be a box in `â„â¿` and `r : â„â¿ â†’ â„` be a function positive on the corresponding closed
box. Then there exists a tagged partition `Ï€` of `I` such that

* `Ï€` is a Henstock partition;
* `Ï€` is subordinate to `r`;
* each box in `Ï€` is homothetic to `I` with coefficient of the form `1 / 2 ^ n`.

This lemma implies that the Henstock filter is nontrivial, hence the Henstock integral is
well-defined. -/
lemma exists_tagged_partition_is_Henstock_is_subordinate_homothetic (I : box Î¹) {r : (Î¹ â†’ â„) â†’ â„}
  (h0 : âˆ€ x âˆˆ I.Icc, 0 < r x) :
  âˆƒ Ï€ : tagged_prepartition I, Ï€.is_partition âˆ§ Ï€.is_Henstock âˆ§ Ï€.is_subordinate r âˆ§
    (âˆ€ J âˆˆ Ï€, âˆƒ n : â„•, âˆ€ i, (J : _).upper i - J.lower i = (I.upper i - I.lower i) / 2 ^ n) âˆ§
    Ï€.distortion = I.distortion :=
begin
  refine box.subbox_induction_on I (Î» J hle hJ, _) (Î» z hz, _),
  { choose! Ï€i hP hHen hr Hn Hd using hJ, choose! n hn using Hn,
    have hP : ((split_center J).bUnion_tagged Ï€i).is_partition,
      from (is_partition_split_center _).bUnion_tagged hP,
    have hsub : âˆ€ (J' âˆˆ (split_center J).bUnion_tagged Ï€i), âˆƒ n : â„•, âˆ€ i,
      (J' : _).upper i - J'.lower i = (J.upper i - J.lower i) / 2 ^ n,
    { intros J' hJ',
      rcases (split_center J).mem_bUnion_tagged.1 hJ' with âŸ¨Jâ‚, hâ‚, hâ‚‚âŸ©,
      refine âŸ¨n Jâ‚ J' + 1, Î» i, _âŸ©,
      simp only [hn Jâ‚ hâ‚ J' hâ‚‚, upper_sub_lower_of_mem_split_center hâ‚, pow_succ,
        div_div_eq_div_mul] },
    refine âŸ¨_, hP, is_Henstock_bUnion_tagged.2 hHen, is_subordinate_bUnion_tagged.2 hr, hsub, _âŸ©,
    refine tagged_prepartition.distortion_of_const _ hP.nonempty_boxes (Î» J' h', _),
    rcases hsub J' h' with âŸ¨n, hnâŸ©,
    exact box.distortion_eq_of_sub_eq_div hn },
  { refine âŸ¨I.Icc âˆ© closed_ball z (r z),
      inter_mem_nhds_within _ (closed_ball_mem_nhds _ (h0 z hz)), _âŸ©,
    intros J Hle n Hmem HIcc Hsub,
    rw set.subset_inter_iff at HIcc,
    refine âŸ¨single _ _ le_rfl _ Hmem, is_partition_single _, is_Henstock_single _,
      (is_subordinate_single _ _).2 HIcc.2, _, distortion_single _ _âŸ©,
    simp only [tagged_prepartition.mem_single, forall_eq],
    refine âŸ¨0, Î» i, _âŸ©, simp }
end

end box

namespace prepartition

open tagged_prepartition finset function

lemma exists_tagged_le_is_Henstock_is_subordinate_Union_eq {I : box Î¹} {r : (Î¹ â†’ â„) â†’ â„}
  (h0 : âˆ€ x âˆˆ I.Icc, 0 < r x) (Ï€ : prepartition I) :
  âˆƒ Ï€' : tagged_prepartition I, Ï€'.to_prepartition â‰¤ Ï€ âˆ§
    Ï€'.is_Henstock âˆ§ Ï€'.is_subordinate r âˆ§ Ï€'.distortion = Ï€.distortion âˆ§
    Ï€'.Union = Ï€.Union :=
begin
  have := Î» J hJ, box.exists_tagged_partition_is_Henstock_is_subordinate_homothetic J
    (Î» x hx, h0 x (box.le_iff_Icc.1 (Ï€.le_of_mem hJ) hx)),
  choose! Ï€i Ï€ip Ï€iH Ï€ir hsub Ï€id, clear hsub,
  refine âŸ¨Ï€.bUnion_tagged Ï€i, bUnion_le _ _, is_Henstock_bUnion_tagged.2 Ï€iH,
    is_subordinate_bUnion_tagged.2 Ï€ir, _, _âŸ©,
  { rw [distortion_bUnion_tagged],
    exact sup_congr rfl Ï€id },
  { rw [Union_bUnion_tagged, Union_def],
    exact Union_congr id surjective_id
      (Î» J, Union_congr id surjective_id $ Î» hJ, (Ï€ip _ hJ).Union_eq) }
end

end prepartition

end box_integral


import analysis.box_integral.partition.filter
import analysis.box_integral.partition.additive
import analysis.normed_space.operator_norm
import topology.uniform_space.compact_separated

open_locale big_operators classical topological_space nnreal filter
open set function filter continuous_linear_map (lsmul)

noncomputable theory

namespace box_integral

variables {Î¹ E F : Type*} [normed_group E] [normed_space â„ E] [normed_group F] [normed_space â„ F]
  {I : box Î¹} {Ï€ : marked_partition I}

namespace partition

def union {I : box Î¹} {i : Î¹} {x : â„} {hx : x âˆˆ Ioo (I.lower i) (I.upper i)}
  (Ï€â‚ : partition (I.split_edge_lower i x hx))
  (Ï€â‚‚ : partition (I.split_edge_upper i x hx)) :
  partition I :=
{ boxes := Ï€â‚.boxes âˆª Ï€â‚‚.boxes,
  finite_boxes := Ï€â‚.finite_boxes.union Ï€â‚‚.finite_boxes,
  bUnion_boxes_coe := by rw [bUnion_union, bUnion_boxes_coe, bUnion_boxes_coe,
    box.union_coe_split_edge],
  pairwise_disjoint :=
    begin
      rintro Jâ‚ (hJâ‚|hJâ‚) Jâ‚‚ (hJâ‚‚|hJâ‚‚) Hne,
      exacts [Ï€â‚.pairwise_disjoint Jâ‚ hJâ‚ Jâ‚‚ hJâ‚‚ Hne,
        (I.disjoint_split_edge i x hx).mono (Ï€â‚.le_of_mem hJâ‚) (Ï€â‚‚.le_of_mem hJâ‚‚),
        (I.disjoint_split_edge i x hx).symm.mono (Ï€â‚‚.le_of_mem hJâ‚) (Ï€â‚.le_of_mem hJâ‚‚),
        Ï€â‚‚.pairwise_disjoint Jâ‚ hJâ‚ Jâ‚‚ hJâ‚‚ Hne]
    end }

end partition

namespace marked_partition

def union {I : box Î¹} {i : Î¹} {x : â„} {hx : x âˆˆ Ioo (I.lower i) (I.upper i)}
  (Ï€â‚ : marked_partition (I.split_edge_lower i x hx))
  (Ï€â‚‚ : marked_partition (I.split_edge_upper i x hx)) :
  marked_partition I :=
{ to_partition := Ï€â‚.to_partition.union Ï€â‚‚.to_partition,
  mark := Î» J, if J âˆˆ Ï€â‚ then Ï€â‚.mark J else Ï€â‚‚.mark J,
  mark_mem_Icc := Î» J,
    begin
      split_ifs,
      exacts [box.le_iff_Icc.1 (I.split_edge_lower_le i x hx) (Ï€â‚.mark_mem_Icc _),
        box.le_iff_Icc.1 (I.split_edge_upper_le i x hx) (Ï€â‚‚.mark_mem_Icc _)]
    end }

lemma is_subordinate.union [fintype Î¹] {I : box Î¹} {i : Î¹} {x : â„}
  {hx : x âˆˆ Ioo (I.lower i) (I.upper i)}
  {Ï€â‚ : marked_partition (I.split_edge_lower i x hx)}
  {Ï€â‚‚ : marked_partition (I.split_edge_upper i x hx)} {r : (Î¹ â†’ â„) â†’ â„}
  (hâ‚ : is_subordinate Ï€â‚ r) (hâ‚‚ : is_subordinate Ï€â‚‚ r) :
  is_subordinate (Ï€â‚.union Ï€â‚‚) r :=
begin
  intros J hJ,
  by_cases hJâ‚ : J âˆˆ Ï€â‚,
  { simp only [union, if_pos hJâ‚, hâ‚ J hJâ‚] },
  { simp only [union, if_neg hJâ‚, hâ‚‚ J (hJ.resolve_left hJâ‚)] }
end

lemma is_Henstock.union [fintype Î¹] {I : box Î¹} {i : Î¹} {x : â„}
  {hx : x âˆˆ Ioo (I.lower i) (I.upper i)}
  {Ï€â‚ : marked_partition (I.split_edge_lower i x hx)}
  {Ï€â‚‚ : marked_partition (I.split_edge_upper i x hx)}
  (hâ‚ : is_Henstock Ï€â‚) (hâ‚‚ : is_Henstock Ï€â‚‚) :
  is_Henstock (Ï€â‚.union Ï€â‚‚) :=
begin
  intros J hJ,
  by_cases hJâ‚ : J âˆˆ Ï€â‚,
  { simp only [union, if_pos hJâ‚, hâ‚ J hJâ‚] },
  { simp only [union, if_neg hJâ‚, hâ‚‚ J (hJ.resolve_left hJâ‚)] }
end

lemma tendsto_union_Henstock [fintype Î¹] {I : box Î¹} {i : Î¹} {x : â„}
  (hx : x âˆˆ Ioo (I.lower i) (I.upper i)) :
  tendsto (Î» Ï€ : marked_partition (I.split_edge_lower i x hx) Ã—
    marked_partition (I.split_edge_upper i x hx), Ï€.1.union Ï€.2)
    (Henstock Ã—á¶  Henstock) Henstock :=
begin
  refine ((has_basis_Henstock.prod has_basis_Henstock).tendsto_iff has_basis_Henstock).2 _,
  refine Î» r hr, âŸ¨(r, r), âŸ¨Î» x hx, hr x _, Î» x hx, hr x _âŸ©, _âŸ©,
  { exact box.le_iff_Icc.1 (I.split_edge_lower_le _ _ _) hx },
  { exact box.le_iff_Icc.1 (I.split_edge_upper_le _ _ _) hx },
  { rintro âŸ¨Ï€â‚, Ï€â‚‚âŸ© âŸ¨âŸ¨hâ‚r, hâ‚âŸ©, âŸ¨hâ‚‚r, hâ‚‚âŸ©âŸ©,
    exact âŸ¨hâ‚r.union hâ‚‚r, hâ‚.union hâ‚‚âŸ© }
end

lemma tendsto_union_Henstock'_aux [fintype Î¹] {I : box Î¹} {i : Î¹} {x : â„}
  (hx : x âˆˆ Ioo (I.lower i) (I.upper i)) (c : â„â‰¥0) :
  tendsto (Î» Ï€ : marked_partition (I.split_edge_lower i x hx) Ã—
    marked_partition (I.split_edge_upper i x hx), Ï€.1.union Ï€.2)
    (Henstock'_aux c Ã—á¶  Henstock'_aux c) (Henstock'_aux c) :=
begin
  rw [Henstock'_aux, Henstock'_aux, â† prod_inf_prod, prod_principal_principal],
  refine (tendsto_union_Henstock hx).inf (tendsto_principal_principal.2 _),
  rintro âŸ¨Ï€â‚, Ï€â‚‚âŸ© âŸ¨hâ‚, hâ‚‚âŸ© J (hJ|hJ),
  exacts [hâ‚ J hJ, hâ‚‚ J hJ]
end

lemma tendsto_union_Riemann [fintype Î¹] {I : box Î¹} {i : Î¹} {x : â„}
  (hx : x âˆˆ Ioo (I.lower i) (I.upper i)) :
  tendsto (Î» Ï€ : marked_partition (I.split_edge_lower i x hx) Ã—
    marked_partition (I.split_edge_upper i x hx), Ï€.1.union Ï€.2)
    (Riemann Ã—á¶  Riemann) Riemann :=
begin
  refine ((has_basis_Riemann.prod has_basis_Riemann).tendsto_iff has_basis_Riemann).2 _,
  refine Î» r hr, âŸ¨(r, r), âŸ¨hr, hrâŸ©, _âŸ©,
  rintro âŸ¨Ï€â‚, Ï€â‚‚âŸ© âŸ¨âŸ¨hâ‚r, hâ‚âŸ©, âŸ¨hâ‚‚r, hâ‚‚âŸ©âŸ©,
  exact âŸ¨hâ‚r.union hâ‚‚r, hâ‚.union hâ‚‚âŸ©
end

end marked_partition

open marked_partition

def integral_sum (f : (Î¹ â†’ â„) â†’ E) (vol : box Î¹ â†’ (E â†’L[â„] F))
  (Ï€ : marked_partition I) : F :=
âˆ‘á¶  J âˆˆ Ï€, vol J (f (Ï€.mark J))

@[simp] lemma integral_sum_add (f g : (Î¹ â†’ â„) â†’ E) (vol : box Î¹ â†’ (E â†’L[â„] F))
  (Ï€ : marked_partition I) :
  integral_sum (f + g) vol Ï€ = integral_sum f vol Ï€ + integral_sum g vol Ï€ :=
begin
  simp only [integral_sum, pi.add_apply, (vol _).map_add],
  exact finsum_mem_add_distrib Ï€.finite_boxes
end

@[simp] lemma integral_sum_neg (f : (Î¹ â†’ â„) â†’ E) (vol : box Î¹ â†’ (E â†’L[â„] F))
  (Ï€ : marked_partition I) :
  integral_sum (-f) vol Ï€ = -integral_sum f vol Ï€ :=
by simp only [integral_sum, pi.neg_apply, (vol _).map_neg, finsum_neg_distrib]

@[simp] lemma integral_sum_smul (c : â„) (f : (Î¹ â†’ â„) â†’ E) (vol : box Î¹ â†’ (E â†’L[â„] F))
  (Ï€ : marked_partition I) :
  integral_sum (c â€¢ f) vol Ï€ = c â€¢ integral_sum f vol Ï€ :=
by simp only [integral_sum, smul_finsum, pi.smul_apply, continuous_linear_map.map_smul]

def has_integral (I : box Î¹) (l : filter (marked_partition I)) (vol : box Î¹ â†’ (E â†’L[â„] F))
  (f : (Î¹ â†’ â„) â†’ E) (y : F) : Prop :=
tendsto (integral_sum f vol) l (ğ“ y)

def integrable (I : box Î¹) (l : filter (marked_partition I)) (vol : box Î¹ â†’ (E â†’L[â„] F))
  (f : (Î¹ â†’ â„) â†’ E) : Prop :=
âˆƒ y, has_integral I l vol f y

def integral (I : box Î¹) (l : filter (marked_partition I)) (vol : box Î¹ â†’ (E â†’L[â„] F))
  (f : (Î¹ â†’ â„) â†’ E) : F :=
if h : integrable I l vol f then classical.some h else 0

variables {l : filter (marked_partition I)}
  {f g : (Î¹ â†’ â„) â†’ E} {vol : box Î¹ â†’ (E â†’L[â„] F)} {y y' : F}

lemma has_integral.tendsto (h : has_integral I l vol f y) :
  tendsto (integral_sum f vol) l (ğ“ y) := h

lemma integrable_iff_cauchy [complete_space F] [ne_bot l] :
  integrable I l vol f â†” cauchy (l.map (integral_sum f vol)) :=
cauchy_map_iff_exists_tendsto.symm

lemma integrable_iff_ex_basis [complete_space F] [ne_bot l] {Î¹' : Sort*} {p : Î¹' â†’ Prop}
  {s : Î¹' â†’ set (marked_partition I)} (h : has_basis l p s) :
  integrable I l vol f â†”
    âˆ€ Îµ > 0, âˆƒ j (hi : p j), âˆ€ Ï€â‚ Ï€â‚‚ âˆˆ s j, âˆ¥integral_sum f vol Ï€â‚ - integral_sum f vol Ï€â‚‚âˆ¥ â‰¤ Îµ :=
by simp only [integrable_iff_cauchy, cauchy_map_iff, â€¹ne_bot lâ€º, true_and,
  h.prod_self.tendsto_iff metric.uniformity_basis_dist_le, prod.forall, set.mem_prod, and_imp,
  mem_set_of_eq, dist_eq_norm, gt_iff_lt]

lemma has_integral.R'_to_R [fintype Î¹] (h : has_integral I Riemann' vol f y) :
  has_integral I Riemann vol f y :=
h.mono_left Riemann_le_Riemann'

lemma has_integral.R_to_H [fintype Î¹] (h : has_integral I Riemann vol f y) :
  has_integral I Henstock vol f y :=
h.mono_left Henstock_le_Riemann

lemma has_integral.R'_to_MS [fintype Î¹] (h : has_integral I Riemann' vol f y) :
  has_integral I McShane vol f y :=
h.mono_left McShane_le_Riemann'

lemma has_integral.MS_to_H [fintype Î¹] (h : has_integral I McShane vol f y) :
  has_integral I Henstock vol f y :=
h.mono_left Henstock_le_McShane

lemma has_integral.H_to_H' [fintype Î¹] (h : has_integral I Henstock vol f y) :
  has_integral I Henstock' vol f y :=
h.mono_left Henstock'_le_Henstock

lemma integrable.has_integral (h : integrable I l vol f) :
  has_integral I l vol f (integral I l vol f) :=
by { rw [integral, dif_pos h], exact classical.some_spec h }

lemma integrable.mono {l'} (h : integrable I l vol f) (hle : l' â‰¤ l) : integrable I l' vol f :=
âŸ¨_, h.has_integral.mono_left hleâŸ©

lemma integrable.R'_to_R [fintype Î¹] (h : integrable I Riemann' vol f) :
  integrable I Riemann vol f :=
h.mono Riemann_le_Riemann'

lemma integrable.R_to_H [fintype Î¹] (h : integrable I Riemann vol f) :
  integrable I Henstock vol f :=
h.mono Henstock_le_Riemann

lemma integrable.R'_to_MS [fintype Î¹] (h : integrable I Riemann' vol f) :
  integrable I McShane vol f :=
h.mono McShane_le_Riemann'

lemma integrable.MS_to_H [fintype Î¹] (h : integrable I McShane vol f) :
  integrable I Henstock vol f :=
h.mono Henstock_le_McShane

lemma integrable.H_to_H' [fintype Î¹] (h : integrable I Henstock vol f) :
  integrable I Henstock' vol f :=
h.mono Henstock'_le_Henstock

lemma has_integral.unique [ne_bot l] (h : has_integral I l vol f y)
  (h' : has_integral I l vol f y') :
  y = y' :=
tendsto_nhds_unique h h'

lemma has_integral.integrable (h : has_integral I l vol f y) : integrable I l vol f := âŸ¨_, hâŸ©

lemma has_integral.integral_eq [ne_bot l] (h : has_integral I l vol f y) :
  integral I l vol f = y :=
h.integrable.has_integral.unique h

lemma has_integral.add (h : has_integral I l vol f y) (h' : has_integral I l vol g y') :
  has_integral I l vol (f + g) (y + y') :=
by simpa only [has_integral, â† integral_sum_add] using h.add h'

lemma integrable.add (hf : integrable I l vol f) (hg : integrable I l vol g) :
  integrable I l vol (f + g) :=
(hf.has_integral.add hg.has_integral).integrable

lemma integral_add [ne_bot l] (hf : integrable I l vol f) (hg : integrable I l vol g) :
  integral I l vol (f + g) = integral I l vol f + integral I l vol g :=
(hf.has_integral.add hg.has_integral).integral_eq

lemma has_integral.neg (hf : has_integral I l vol f y) : has_integral I l vol (-f) (-y) :=
by simpa only [has_integral, â† integral_sum_neg] using hf.neg

lemma integrable.neg (hf : integrable I l vol f) : integrable I l vol (-f) :=
hf.has_integral.neg.integrable

lemma integrable.of_neg (hf : integrable I l vol (-f)) : integrable I l vol f := neg_neg f â–¸ hf.neg

@[simp] lemma integrable_neg : integrable I l vol (-f) â†” integrable I l vol f :=
âŸ¨Î» h, h.of_neg, Î» h, h.negâŸ©

@[simp] lemma integral_neg [ne_bot l] : integral I l vol (-f) = -integral I l vol f :=
if h : integrable I l vol f then h.has_integral.neg.integral_eq
else by rw [integral, integral, dif_neg h, dif_neg (mt integrable.of_neg h), neg_zero]

lemma has_integral.sub (h : has_integral I l vol f y) (h' : has_integral I l vol g y') :
  has_integral I l vol (f - g) (y - y') :=
by simpa only [sub_eq_add_neg] using h.add h'.neg

lemma integrable.sub (hf : integrable I l vol f) (hg : integrable I l vol g) :
  integrable I l vol (f - g) :=
(hf.has_integral.sub hg.has_integral).integrable

lemma integral_sub [ne_bot l] (hf : integrable I l vol f) (hg : integrable I l vol g) :
  integral I l vol (f - g) = integral I l vol f - integral I l vol g :=
(hf.has_integral.sub hg.has_integral).integral_eq

lemma has_integral_zero : has_integral I l vol (Î» _, (0:E)) 0 :=
by { dunfold has_integral, convert tendsto_const_nhds, ext Ï€, simp [integral_sum] }

lemma integrable_zero : integrable I l vol (Î» _, (0:E)) := âŸ¨0, has_integral_zeroâŸ©

@[simp] lemma integral_zero [ne_bot l] : integral I l vol (Î» _, (0:E)) = 0 :=
has_integral_zero.integral_eq

lemma has_integral_sum {Î± : Type*} {s : finset Î±} {f : Î± â†’ (Î¹ â†’ â„) â†’ E} {g : Î± â†’ F}
  (h : âˆ€ i âˆˆ s, has_integral I l vol (f i) (g i)) :
  has_integral I l vol (Î» x, âˆ‘ i in s, f i x) (âˆ‘ i in s, g i) :=
begin
  induction s using finset.induction_on with a s ha ihs, { simp [has_integral_zero] },
  simp only [finset.sum_insert ha], rw finset.forall_mem_insert at h,
  exact h.1.add (ihs h.2)
end

lemma has_integral.smul (hf : has_integral I l vol f y) (c : â„) :
  has_integral I l vol (c â€¢ f) (c â€¢ y) :=
by simpa only [has_integral, â† integral_sum_smul]
  using (tendsto_const_nhds : tendsto _ _ (ğ“ c)).smul hf

lemma integrable.smul (hf : integrable I l vol f) (c : â„) :
  integrable I l vol (c â€¢ f) :=
(hf.has_integral.smul c).integrable

lemma integrable.of_smul {c : â„} (hf : integrable I l vol (c â€¢ f)) (hc : c â‰  0) :
  integrable I l vol f :=
by { convert hf.smul câ»Â¹, ext x, simp only [pi.smul_apply, inv_smul_smul' hc] }

@[simp] lemma integral_smul [ne_bot l] (c : â„) :
  integral I l vol (Î» x, c â€¢ f x) = c â€¢ integral I l vol f :=
begin
  rcases eq_or_ne c 0 with rfl | hc, { simp },
  by_cases hf : integrable I l vol f,
  { exact (hf.has_integral.smul c).integral_eq },
  { have : Â¬integrable I l vol (Î» x, c â€¢ f x), from mt (Î» h, h.of_smul hc) hf,
    rw [integral, integral, dif_neg hf, dif_neg this, smul_zero] }
end

lemma integral_sum_bUnion_unmarked [fintype Î¹] (Ï€ : marked_partition I) (Ï€i : Î  J âˆˆ Ï€, partition J)
  (hvol : box_additive_on vol I):
  integral_sum f vol (Ï€.bUnion_unmarked Ï€i) = integral_sum f vol Ï€ :=
begin
  refine (Ï€.to_partition.finsum_mem_bUnion Ï€i _).trans _,
  refine finsum_congr (Î» J, finsum_congr $ Î» hJ, _),
  have : âˆ€ x, box_additive_on (Î» J', vol J' x) J,
    from Î» x, (hvol.add_monoid_hom_comp (continuous_linear_map.apply â„ F x)
      .to_linear_map.to_add_monoid_hom).restrict (Ï€.to_partition.le_of_mem hJ),
  calc âˆ‘á¶  J' âˆˆ Ï€i J hJ, vol J' (f (Ï€.mark (Ï€.to_partition.bUnion_index Ï€i J'))) =
    âˆ‘á¶  J' âˆˆ Ï€i J hJ, vol J' (f (Ï€.mark J)) :
      finsum_mem_congr rfl (Î» J' (hJ' : J' âˆˆ Ï€i J hJ), by rw partition.bUnion_index_of_mem _ _ hJ')
  ... = vol J (f (Ï€.mark J)) : (this _).finsum_mem_partition _
end

lemma integral_sum_inf_unmarked [fintype Î¹] (Ï€ : marked_partition I) (Ï€' : partition I)
  (hvol : box_additive_on vol I):
  integral_sum f vol (Ï€.inf_unmarked Ï€') = integral_sum f vol Ï€ :=
integral_sum_bUnion_unmarked Ï€ _ hvol

@[simp] lemma integral_sum_union {i : Î¹} {x : â„} {hx : x âˆˆ Ioo (I.lower i) (I.upper i)}
  (Ï€â‚ : marked_partition (I.split_edge_lower i x hx))
  (Ï€â‚‚ : marked_partition (I.split_edge_upper i x hx)) :
  integral_sum f vol (Ï€â‚.union Ï€â‚‚) = integral_sum f vol Ï€â‚ + integral_sum f vol Ï€â‚‚ :=
begin
  have : disjoint Ï€â‚.boxes Ï€â‚‚.boxes,
  { rintro J âŸ¨hâ‚, hâ‚‚âŸ©,
    refine (Ï€â‚.to_partition.le_of_mem hâ‚ J.upper_mem i).2.not_lt _,
    simpa using (Ï€â‚‚.to_partition.le_of_mem hâ‚‚ J.upper_mem i).1 },
  simp only [integral_sum],
  refine (finsum_mem_union this Ï€â‚.finite_boxes Ï€â‚‚.finite_boxes).trans _,
  refine congr_arg2 (+) (finsum_mem_congr rfl $ Î» J hJ, _) (finsum_mem_congr rfl $ Î» J hJ, _);
    congr' 2,
  exacts [if_pos hJ, if_neg (Î» h, this âŸ¨h, hJâŸ©)]
end

lemma integral_sum_sub_partitions [fintype Î¹] (Ï€â‚ Ï€â‚‚ : marked_partition I)
  (hvol : box_additive_on vol I) :
  integral_sum f vol Ï€â‚ - integral_sum f vol Ï€â‚‚ =
    âˆ‘á¶  J âˆˆ Ï€â‚.to_partition âŠ“ Ï€â‚‚.to_partition,
      (vol J (f $ (Ï€â‚.inf_unmarked Ï€â‚‚.to_partition).mark J) -
        vol J (f $ (Ï€â‚‚.inf_unmarked Ï€â‚.to_partition).mark J)) :=
begin
  erw [â† integral_sum_inf_unmarked Ï€â‚ Ï€â‚‚.to_partition hvol,
    â† integral_sum_inf_unmarked Ï€â‚‚ Ï€â‚.to_partition hvol, integral_sum, integral_sum,
    finsum_mem_sub_distrib (partition.finite_boxes _)],
  congr' 1,
  rw inf_comm, refl
end

lemma has_integral_const [fintype Î¹] (hvol : box_additive_on vol I) (c : E) :
  has_integral I l vol (Î» _, c) (vol I c) :=
begin
  refine tendsto_const_nhds.congr (Î» Ï€, _),
  rw [integral_sum, eq_comm],
  refine box_additive_on.finsum_mem_partition _ Ï€.to_partition,
  exact hvol.add_monoid_hom_comp (continuous_linear_map.apply â„ F c).to_linear_map.to_add_monoid_hom
end

lemma integral_const [fintype Î¹] [ne_bot l] (hvol : box_additive_on vol I) (c : E) :
  integral I l vol (Î» _, c) = vol I c :=
(has_integral_const hvol c).integral_eq

lemma integrable_const [fintype Î¹] (hvol : box_additive_on vol I) (c : E) :
  integrable I l vol (Î» _, c) :=
âŸ¨_, has_integral_const hvol câŸ©

def volume [fintype Î¹] (I : box Î¹) : E â†’L[â„] E := lsmul â„ â„ I.volume

@[simp] lemma volume_apply [fintype Î¹] (I : box Î¹) (x : E) : volume I x = I.volume â€¢ x := rfl

lemma box_additive_on_volume [fintype Î¹] (I : box Î¹) :
  box_additive_on (volume : box Î¹ â†’ E â†’L[â„] E) I :=
(box_additive_on_box_volume I).add_monoid_hom_comp
      (lsmul â„ â„ : â„ â†’L[â„] E â†’L[â„] E).to_linear_map.to_add_monoid_hom

lemma norm_integral_le_of_le_const [fintype Î¹] [ne_bot l] {c : â„} (hc : âˆ€ x âˆˆ I.Icc, âˆ¥f xâˆ¥ â‰¤ c) :
  âˆ¥integral I l volume fâˆ¥ â‰¤ I.volume * c :=
begin
  have h0 : 0 â‰¤ c, from (norm_nonneg _).trans (hc I.upper I.upper_mem_Icc),
  by_cases hf : integrable I l volume f,
  { refine le_of_tendsto' hf.has_integral.norm (Î» Ï€, _),
    erw [integral_sum, Ï€.to_partition.finsum_eq_sum],
    rw [â† (box_additive_on_box_volume I).finsum_mem_partition Ï€.to_partition,
      partition.finsum_eq_sum, finset.sum_mul],
    refine norm_sum_le_of_le _ (Î» J hJ, _), rw finite.mem_to_finset at hJ,
    rw [volume_apply, norm_smul, real.norm_eq_abs, abs_of_pos J.volume_pos,
      mul_le_mul_left J.volume_pos],
    exact hc _ (Ï€.mark_mem_Icc _) },
  { rw [integral, dif_neg hf, norm_zero],
    exact mul_nonneg I.volume_pos.le h0 }
end

lemma box_additive_on_integral_Riemann [fintype Î¹]
  (H : âˆ€ J â‰¤ I, integrable J Riemann volume f) :
  box_additive_on (Î» J, integral J Riemann volume f) I :=
begin
  intros J hJ i x hx, simp only,
  have A := (H J hJ).has_integral.tendsto.comp (tendsto_union_Riemann hx),
  simp only [(âˆ˜), integral_sum_union] at A,
  have B := ((H (J.split_edge_lower i x hx) _).has_integral.tendsto.comp tendsto_fst).add
    ((H (J.split_edge_upper i x hx) _).has_integral.tendsto.comp tendsto_snd),
  exacts [tendsto_nhds_unique B A, (J.split_edge_lower_le _ _ _).trans hJ,
    (J.split_edge_upper_le _ _ _).trans hJ]
end

lemma box_additive_on_integral_Henstock [fintype Î¹]
  (H : âˆ€ J â‰¤ I, integrable J Henstock volume f) :
  box_additive_on (Î» J, integral J Henstock volume f) I :=
begin
  intros J hJ i x hx,
  have A := (H J hJ).has_integral.tendsto.comp (tendsto_union_Henstock hx),
  simp only [(âˆ˜), integral_sum_union] at A,
  have B := ((H (J.split_edge_lower i x hx) _).has_integral.tendsto.comp tendsto_fst).add
    ((H (J.split_edge_upper i x hx) _).has_integral.tendsto.comp tendsto_snd),
  exacts [tendsto_nhds_unique B A, (J.split_edge_lower_le _ _ _).trans hJ,
    (J.split_edge_upper_le _ _ _).trans hJ]
end

lemma box_additive_on_integral_Henstock' [fintype Î¹]
  (H : âˆ€ J â‰¤ I, integrable J Henstock' volume f) :
  box_additive_on (Î» J, integral J Henstock' volume f) I :=
begin
  intros J hJ i x hx,
  set c := max (J.split_edge_lower i x hx).distortion (J.split_edge_upper i x hx).distortion,
  haveI : (Henstock'_aux c).ne_bot := Henstock'_aux_ne_bot (le_max_left _ _),
  haveI : (Henstock'_aux c).ne_bot := Henstock'_aux_ne_bot (le_max_right _ _),
  replace H : âˆ€ J â‰¤ I, tendsto (integral_sum f volume) (Henstock'_aux c)
    (ğ“ (integral J Henstock' volume f)) := Î» J hJ, tendsto_supr.1 ((H J hJ).has_integral.tendsto) c,
  have A := (H J hJ).comp (tendsto_union_Henstock'_aux hx c),
  simp only [(âˆ˜), integral_sum_union] at A,
  have B := H _ ((J.split_edge_lower_le _ _ hx).trans hJ),
  have C := H _ ((J.split_edge_upper_le _ _ hx).trans hJ),
  exact tendsto_nhds_unique ((B.comp tendsto_fst).add (C.comp tendsto_snd)) A
end

lemma Riemann'_integrable_of_continuous_on [fintype Î¹] [complete_space E]
  {I : box Î¹} {f : (Î¹ â†’ â„) â†’ E} (hc : continuous_on f I.Icc) :
  integrable I Riemann' volume f :=
begin
  have huc := (is_compact_pi_Icc I.lower I.upper).uniform_continuous_on_of_continuous hc,
  rw metric.uniform_continuous_on_iff_le at huc,
  refine (integrable_iff_ex_basis has_basis_Riemann').2 (Î» Îµ Îµ0, _),
  rcases huc (Îµ / I.volume) (div_pos Îµ0 I.volume_pos) with âŸ¨Î´, Î´0 : 0 < Î´, HÎ´âŸ©,
  use [Î´ / 2, half_pos Î´0],
  rintros Ï€â‚ Ï€â‚‚ hâ‚ hâ‚‚,
  simp_rw [integral_sum_sub_partitions _ _ (box_additive_on_volume I),
    â† continuous_linear_map.map_sub, partition.finsum_eq_sum, volume_apply],
  have : âˆ€ J âˆˆ (Ï€â‚.to_partition âŠ“ Ï€â‚‚.to_partition).finite_boxes.to_finset,
    âˆ¥(J : _).volume â€¢ (f ((Ï€â‚.inf_unmarked Ï€â‚‚.to_partition).mark J) -
      f ((Ï€â‚‚.inf_unmarked Ï€â‚.to_partition).mark J))âˆ¥ â‰¤ J.volume * (Îµ / I.volume),
  { intros J hJ,
    rw [finite.mem_to_finset] at hJ,
    rw [norm_smul, real.norm_eq_abs, abs_of_pos J.volume_pos, mul_le_mul_left J.volume_pos,
      â† dist_eq_norm],
    refine HÎ´ _ _ (marked_partition.mark_mem_Icc _ _) (marked_partition.mark_mem_Icc _ _) _,
    rw [â† add_halves Î´],
    refine (dist_triangle_left _ _ J.upper).trans (add_le_add (hâ‚ _ _ _) (hâ‚‚ _ _ _)),
    { apply partition.bUnion_index_mem },
    { exact (@box.Icc Î¹).monotone (partition.le_bUnion_index _ hJ) J.upper_mem_Icc },
    { apply partition.bUnion_index_mem },
    { rw inf_comm at hJ,
      exact (@box.Icc Î¹).monotone (partition.le_bUnion_index _ hJ) J.upper_mem_Icc } },
  refine (norm_sum_le_of_le _ this).trans _,
  rw [â† finset.sum_mul, â† partition.finsum_eq_sum,
    (box_additive_on_box_volume I).finsum_mem_partition, mul_div_cancel' _ I.volume_pos.ne']
end

lemma Henstock_integrable_of_continuous_on [fintype Î¹] [complete_space E]
  {I : box Î¹} {f : (Î¹ â†’ â„) â†’ E} (hc : continuous_on f I.Icc) :
  integrable I Henstock volume f :=
(Riemann'_integrable_of_continuous_on hc).R'_to_MS.MS_to_H

lemma Henstock'_integrable_of_continuous_on [fintype Î¹] [complete_space E]
  {I : box Î¹} {f : (Î¹ â†’ â„) â†’ E} (hc : continuous_on f I.Icc) :
  integrable I Henstock' volume f :=
(Henstock_integrable_of_continuous_on hc).H_to_H'

lemma has_integral_McShane_inf_principal_of_forall_is_o [fintype Î¹] (B : box Î¹ â†’ â„)
  (HB : box_additive_on B I) (g : box Î¹ â†’ F) (hg : box_additive_on g I) (p : (Î¹ â†’ â„) â†’ box Î¹ â†’ Prop)
  (H : âˆ€ (x âˆˆ I.Icc) (Îµ > 0), âˆƒ Î´ > 0, âˆ€ J â‰¤ I, J.Icc âŠ† metric.closed_ball x Î´ â†’ p x J â†’
    dist (vol J (f x)) (g J) â‰¤ Îµ * B J) :
  has_integral I (McShane âŠ“ ğ“Ÿ {Ï€ | âˆ€ J âˆˆ Ï€, p (Ï€.mark J) J}) vol f (g I) :=
begin
  refine ((has_basis_McShane.inf_principal _).tendsto_iff metric.nhds_basis_closed_ball).2 _,
  intros Îµ Îµ0,
  choose! Î´ Î´0 HÎ´Îµ using H, simp only [dist_eq_norm] at HÎ´Îµ,
  have Hpos : 0 < max (B I) 1, from lt_max_iff.2 (or.inr zero_lt_one),
  refine âŸ¨Î» x, Î´ x (Îµ / max (B I) 1), Î» x hx, Î´0 x hx _ (div_pos Îµ0 Hpos), _âŸ©,
  rintro Ï€ âŸ¨hÏ€Î´, hÏ€pâŸ©, rw mem_set_of_eq at hÏ€Î´ hÏ€p,
  erw [metric.mem_closed_ball, â† hg.finsum_mem_partition Ï€.to_partition, dist_eq_norm, integral_sum,
   Ï€.to_partition.finsum_eq_sum, Ï€.to_partition.finsum_eq_sum, â† finset.sum_sub_distrib],
  have : âˆ€ J âˆˆ Ï€.finite_boxes.to_finset, âˆ¥vol J (f $ Ï€.mark J) - g Jâˆ¥ â‰¤ Îµ / max (B I) 1 * B J,
  { intros J hJ,
    rw finite.mem_to_finset at hJ,
    exact HÎ´Îµ _ (Ï€.mark_mem_Icc _) _ (div_pos Îµ0 Hpos) _ (Ï€.to_partition.le_of_mem hJ) (hÏ€Î´ J hJ)
      (hÏ€p _ hJ) },
  refine (norm_sum_le_of_le _ this).trans _,
  rw [â† finset.mul_sum, â† Ï€.to_partition.finsum_eq_sum, HB.finsum_mem_partition],
  rw [div_mul_eq_mul_div, div_le_iff Hpos],
  exact mul_le_mul_of_nonneg_left (le_max_left _ _) Îµ0.le
end

lemma has_integral_Henstock'_of_forall_is_o [fintype Î¹] (B : box Î¹ â†’ â„) (HB : box_additive_on B I)
  (g : box Î¹ â†’ F) (hg : box_additive_on g I)
  (H : âˆ€ (c : â„â‰¥0) (x âˆˆ I.Icc) (Îµ > 0), âˆƒ Î´ > 0, âˆ€ J â‰¤ I, J.Icc âŠ† metric.closed_ball x Î´ â†’
    x âˆˆ J.Icc â†’ J.distortion â‰¤ c â†’ dist (vol J (f x)) (g J) â‰¤ Îµ * B J) :
  has_integral I Henstock' vol f (g I) :=
begin
  rw [Henstock'_def, has_integral, tendsto_supr],
  intro c, rw â† has_integral,
  convert has_integral_McShane_inf_principal_of_forall_is_o B HB g hg
    (Î» x J, x âˆˆ J.Icc âˆ§ J.distortion â‰¤ c) _,
  simpa only [and_imp] using H c
end

end box_integral
